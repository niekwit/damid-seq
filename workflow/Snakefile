include: "scripts/general_functions.smk"
from scripts.resources import Resources
from snakemake.utils import min_version, validate

report: "report/workflow.rst"

# Workflow version
VERSION = "v0.5.0"

# Set minimum snakemake version
min_version("8.10.6")

# Wrapper version
wrapper_version = "v3.10.2"

# Load and validate config file
configfile: "config/config.yaml"
validate(config, "schemas/config.schema.yaml")

plasmid_name = plasmid_fasta_name(config["plasmid_fasta"])

# Load sample information
csv = pd.read_csv("config/samples.csv")

# Load genome resources to be used in rules
resources = Resources(config["genome"], config["ensembl_genome_build"])

# Get paired-end status and bam extension
paired_end = paired_end()

# Get fusion protein (to be masked in fasta file)
maskedgenes = masked_genes()

# Get data type
if data_type() == "matrix":
    matrix_samples()

DIRS = dirs()
SAMPLES = samples() # Includes Dam control
BG_SAMPLES = samples(bedgraph=True) # SAMPLES minus Dam control
DAM_SAMPLES = samples(bedgraph=True, dam=True)
 
# Import rules
include: "rules/fastqc.smk"
include: "rules/trimming.smk"
include: "rules/resources.smk"
include: "rules/damid.smk"
include: "rules/bedgraph_processing.smk"
include: "rules/deeptools.smk"
include: "rules/plotting.smk"
include: "rules/peak_calling.smk"
include: "rules/bed.smk"

# Target rule
rule all:
    input: 
        targets()

# Save snakemake terminal output to log file
time = datetime.datetime.now().strftime("%Y-%m-%d_%H-%M-%S")
snake_log = f"logs/snakemake/{time}_snakemake.log"
os.makedirs("logs/snakemake", exist_ok=True)

onsuccess: 
    shell("cp -v {log} {snake_log}")
    print("Analysis finished successfully!")
onerror:
    shell("cp -v {log} {snake_log}")
    print(f"Analysis failed...\nCheck {snake_log} for details")
