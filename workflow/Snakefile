import os
import glob
import sys
import pandas as pd
import datetime
from scripts.resources import Resources
include: "scripts/general_functions.smk"
from snakemake.utils import min_version
from snakemake.utils import validate

report: "report/workflow.rst"

# Set minimum snakemake version
min_version("7.25.0")

# load config file
configfile: "config/config.yaml"
validate(config, "schemas/config.schema.yaml")

# Load sample information
csv = pd.read_csv("config/samples.csv")

# Load genome resources to be used in rules
resources = Resources(config["genome"], config["ensembl_genome_build"])

# Get paired-end status and bam extension
paired_end, BAM_EXT = paired_end()

# Get fusion protein (to be masked in fasta file)
maskedgenes = masked_genes()

# Get sample directories
if sample_type() == "paired":
    DIRS = dirs()
    SAMPLES = paired_samples()
    BG_SAMPLES = paired_samples(bedgraph=True) # SAMPLES minus Dam control
    DAM_SAMPLES = paired_samples(bedgraph=True, dam=True)
elif sample_type() == "matrix":
    pass

# Import rules
include: "rules/fastqc.smk"
include: "rules/trimming.smk"
include: "rules/resources.smk"
include: "rules/damid.smk"
include: "rules/bedgraph_processing.smk"
include: "rules/deeptools.smk"
include: "rules/plotting.smk"
include: "rules/peak_calling.smk"
include: "rules/bed.smk"
#include: "rules/motifs.smk"

# Target rule
rule all:
    input: 
        targets()

# Save snakemake terminal output to log file
time = datetime.datetime.now().strftime("%Y-%m-%d_%H-%M-%S")
snake_log = f"logs/snakemake/{time}_snakemake.log"
os.makedirs("logs/snakemake", exist_ok=True)

onsuccess: 
    shell("cp -v {log} {snake_log}")
    print("Analysis finished successfully!")

onerror:
    shell("cp -v {log} {snake_log}")
    print(f"Analysis failed...\nCheck {snake_log} for details")
